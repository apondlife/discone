// -----------
// -- Intro --
// -----------

title: Intro
---
hello? wow. how did you already know to hold "right or left trigger" to,
<<wait 60>>
===

title: IntroInput
---
<<then Mechanic IntroEnd>>

close your eyes?
<<wait 6>>

and then release "right or left trigger" to open them?
<<wait 60>>
===

title: IntroEnd
---
===

// -----------
// -- Start --
// -----------

title: Start
---
<<then Mechanic StartThanks>>
<<wait 10>>

if you want to talk to me again
<<wait 3>>

you can hold "right or left trigger"
<<wait 3>>

& close your eyes for a little.
<<wait 3>>
===

title: StartThanks
---
<<wait 2>>
<<then Mechanic Birthplace>>

ah, thanks for holding my “button”!
<<wait 3>>

sorry to bother you there, earlier.
<<wait 3>>

i'm always here if you want to talk.
<<wait 60>>
===

// ----------------
// -- Birthplace --
// ----------------

title: Birthplace
---
<<declare $BirthplaceStep = "Ramp">>
<<declare $BirthplaceRamp_Help_Visited = false>>
<<declare $BirthplaceStair_Born_Visited = false>>
<<declare $BirthplaceWall_Help_Visited = false>>

<<if $BirthplaceStep == "Ramp">>
  <<jump BirthplaceRamp>>
<<elseif $BirthplaceStep == "Stair">>
  <<jump BirthplaceStair>>
<<elseif $BirthplaceStep == "Wall">>
  <<jump BirthplaceWall>>
<<elseif $BirthplaceStep == "Tunnel">>
  <<jump BirthplaceWall>>
<<else>>
  <<jump BirthplaceExit>>
<<endif>>
===

title: BirthplaceRamp
---
<<if !$BirthplaceRamp_Help_Visited>>
  <<jump BirthplaceRamp_Letter>>
<<else>>
  <<jump BirthplaceRamp_Help>>
<<endif>>
===

title: BirthplaceRamp_Letter
---
<<wait 2>>

this ramp, did you...
<<wait 3>>

see my letter too?
<<jump BirthplaceRamp_Help>>
===

title: BirthplaceRamp_Help
---
<<set $BirthplaceRamp_Help_Visited to true>>
<<wait 2>>

if you mash the "jump button" as you approach...
<<wait 3>>

it,
<<wait 1>>

i think you can do it
<<wait 60>>
===

title: BirthplaceStair
---
<<if !$BirthplaceStair_Born_Visited && $BirthplaceRamp_Help_Visited>>
  <<jump BirthplaceStair_See>>
<<else>>
  <<jump BirthplaceStair_Born>>
<<endif>>
===

title: BirthplaceStair_See
---
<<wait 2>>
that was beautiful...

<<jump BirthplaceStair_Born>>
===

title: BirthplaceStair_Born
---
<<set $BirthplaceStair_Born_Visited to true>>
<<wait 2>>

i remember when this stair was born.
<<wait 60>>
===

title: BirthplaceWall
---
<<if !$BirthplaceWall_Help_Visited>>
  <<jump BirthplaceWall_Feet>>
<<else>>
  <<jump BirthplaceWall_Help>>
<<endif>>
===

title: BirthplaceWall_Feet
---
<<wait 2>>

a wall is a nice place...
<<wait 2>>

to press your feet into,
<<jump BirthplaceWall_Help>>
===

title: BirthplaceWall_Help
---
<<set $BirthplaceRamp_Help_Visited to true>>
<<wait 2>>

to hold the "jump button", along
<<wait 2>>

like any other floor.
<<wait 60>>
===

title: BirthplaceTunnel
---
<<if $BirthplaceWall_Help_Visited>>
  <<jump BirthplaceTunnel_See>>
<<endif>>
===

title: BirthplaceTunnel_See
---
<<wait 2>>

how are your feet?
<<wait 60>>
===

title: BirthplaceExit
---
<<then Mechanic IntroReal>>
<<jump IntroReal>>
===

// ---------------
// -- IntroReal --
// ---------------

title: IntroReal
---
<<wait 2>>
<<then Mechanic IntroRealNormal>>

this is the place...
<<wait 2>>

do you know what you're doing Here?
<<wait 60>>
===

title: IntroRealNormal
---
<<wait 2>>
<<then Mechanic IntroRealSoon>>

don't worry if you don't, that's normal.
<<wait 2>>

we never really do.
<<wait 60>>
===

title: IntroRealSoon
---
<<wait 2>>
<<then Mechanic IntroRealAsk>>

but anyways i'm sure you'll figure it out...
<<wait 2>>

what you're doing Here
<<wait 2>>

soon.
<<wait  60>>
===

title: IntroRealAsk
---
<<wait 2>>
<<then Mechanic IntroRealTell>>

don't ask me though, i couldn't tell you.
<<wait  60>>
===

title: IntroRealTell
---
<<wait 2>>
<<then Mechanic IntroRealMechanic>>

i can't tell you, even.
<<wait  60>>
===

title: IntroRealMechanic
---
<<wait 2>>
<<then Mechanic IntroRealWant>>

& speaking of asking me, i should...
<<wait 2>>

introduce myself. you could call me
<<wait 2>>

“the mechanic”,
<<wait 2>>

if you ever get the chance =).
<<wait  60>>
===

title: IntroRealWant
---
<<wait 2>>
<<then Mechanic IntroRealEyes>>

but if you do want to hear me, i'm usually around here.
<<wait  60>>
===

title: IntroRealEyes
---
<<wait 2>>
<<then Mechanic IntroRealEnd>>

if you don't, well, close your eyes i guess ;).
<<wait  60>>
===

title: IntroRealEnd
---
===
